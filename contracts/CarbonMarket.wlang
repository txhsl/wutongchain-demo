contract CarbonMarket {

    public string name = "CarbonMarket"

    public uint8 decimals = 4

    struct Archive {
        int MEA
        float MEP
        float MEC
        float MESFC

        float AEP
        float AEC
        float AESFC

        int capacity
        float velocity

        float coefficient
    }

    struct Required {
        int X
        float a
        int b
        float c
        float coefficient
    }

    public string getName() {
        return name
    }

    public uint8 getDecimals() {
        return decimals
    }

    public string register(string account, int X, float a, int b, float c, float coefficient) {
        Required req
        req.X = X
        req.a = a
        req.b = b
        req.c = c
        req.coefficient = coefficient
        string json = obj_to_json(req)
        db_set(account + "_EEXIA", json)
        event(name + "_register", account)
        return "success"
    }

    public string updateArchive(string account, int MEA, float MEP, float MEC, float MESFC, float AEP, float AEC, float AESFC, int capacity, float velocity) {
        Archive arc
        arc.MEA = MEA
        arc.MEP = MEP
        arc.MEC = MEC
        arc.MESFC = MESFC
        arc.AEP = AEP
        arc.AEC = AEC
        arc.AESFC = AESFC
        arc.capacity = capacity
        arc.velocity = velocity
        arc.coefficient = (MEA * MEP * MEC * MESFC + AEP * AEC * AESFC) / (capacity * velocity)

        string json = obj_to_json(arc)
        db_set(account + "_EEXIB", json)
        event(name + "_update", account)
        return "success"
    }

    public float getRequiredEEXI(string account) {
        string json = db_get<string>(account + "_EEXIA")
        Required req = json_to_obj<Required>(json)
        return req.coefficient
    }

    public float getCurrentEEXI(string account) {
        string json = db_get<string>(account + "_EEXIB")
        Archive arc = json_to_obj<Archive>(json)
        return arc.coefficient
    }

    public float getFactor(string account) {
        string jsonA = db_get<string>(account + "_EEXIA")
        string jsonB = db_get<string>(account + "_EEXIB")
        Required req = json_to_obj<Required>(jsonA)
        Archive arc = json_to_obj<Archive>(jsonB)
        return req.coefficient - arc.coefficient
    }

    public string settleAccount(string account, int distance) {
        float factor = getFactor(account)
        float balance = db_get<float>(account + "_balance")

        balance += factor * distance

        db_set(account + "_balance", balance)
        event(name + "_settle", account)
        return "success"
    }

    public float balanceOf(string account) {
        return db_get<float>(account + "_balance")
    }

    public string transfer(string from, string to, int amount) {
        float balanceA = db_get<float>(from + "_balance")
        float balanceB = db_get<float>(to + "_balance")

        if(balanceA >= amount && balanceB + amount > balanceB) {
            balanceA = balanceA - amount
            balanceB = balanceB + amount

            db_set(from + "_balance", balanceA)
            db_set(to + "_balance", balanceB)
            event(name + "_transfer", to)
        }
        else {
            return "failed"
        }
        return "success"
    }
}